BEGIN ;
INCLUDE 'lnCOSY91';
PROCEDURE RUN ;

   PROCEDURE M5_POTFLD_OUT  X Y S   B_X B_Y B_Z ;
    {M5 field with z=0 as center of element}
     VARIABLE XARR 2 30 ; VARIABLE ZENGE 1 ;
     XARR(1) := X ; XARR(3) := Y ;
     LCCT := 1 ;  {set to 1, plugin x,y values in the multipole expansion}
     IF LFR=4 ;
       NSDP := 19 ; ZENGE := -1*(S-PPOL(21)/2) ;
     ELSEIF 1=1 ;
      {Assuming that S=0 is the middle of the element, the entrance and exit 
       field are switched at this point.}
       IF S<0 ;     NSDP := 1 ; ZENGE := (S+PPOL(21)/2) ;
       ELSEIF 1=1 ; NSDP := 2 ; ZENGE := (S-PPOL(21)/2) ; ENDIF ;
     ENDIF ;
     LCCT := 1 ;  {set to 1, plugin x,y values in the multipole expansion}
     POTFLD XARR ZENGE ;
     B_X := CONS(BFX) ; B_Y := CONS(BFY) ; B_Z := CONS(BFZ) ;
   ENDPROCEDURE ;

  PROCEDURE GENERATE_M5_FIELD_EXT ;
  {M5 field with z=0 as center of element (instead of entrance of field bound.)
   Evaluate BX BY BZ field components at a set of X Y Z points that are 
   specified from file
     TMP_Enge_params_for_COSY.txt
   This file also specifies M5 model parameters (LEFF, RADIUS, Enge coefs 
   and desired max order ORD of the calculation.
   }
   VARIABLE ORD 1 ; VARIABLE LEFF 1 ; VARIABLE RADIUS 1 ;
   VARIABLE IMP 1 ; VARIABLE IEE 1 ; VARIABLE IEM 1 ; VARIABLE C 1 12 ;
   VARIABLE TMP 1 ; VARIABLE STR0 512 ; VARIABLE XYZ 1 3 ; VARIABLE NPNTS 1 ;
   {}
   VARIABLE F1 1 ; VARIABLE F2 1 ; VARIABLE F3 1 ; VARIABLE F4 1 ;
   VARIABLE F5 1 ; VARIABLE F6 1 ;
   VARIABLE I 1 ; VARIABLE J 1 ; VARIABLE K 1 ;
   VARIABLE X 1 ; VARIABLE Y 1 ; VARIABLE Z 1 ;
   VARIABLE TH 1 ; VARIABLE RD 1 ;
   VARIABLE BX 1 ; VARIABLE BY 1 ; VARIABLE BZ 1 ;
   VARIABLE BR 1 ; VARIABLE BT 1 ; VARIABLE C_ 1 ; VARIABLE S_ 1 ;
   VARIABLE FN 1 ; VARIABLE LINE 256 ;
   VARIABLE ZMIN 1 ; VARIABLE ZMAX 1 ; VARIABLE DZ 1 ; VARIABLE N_Z 1 ;
   VARIABLE XMIN 1 ; VARIABLE XMAX 1 ; VARIABLE DX 1 ; VARIABLE N_X 1 ;
   VARIABLE THMIN 1 ; VARIABLE THMAX 1 ; VARIABLE DTH 1 ; VARIABLE N_TH 1 ;
   VARIABLE RDMIN 1 ; VARIABLE RDMAX 1 ; VARIABLE DRD 1 ; VARIABLE N_RD 1 ;
   VARIABLE N_TOT 1 ;
   VARIABLE DIR_OUT 400 ; VARIABLE F_OUT 100 10 ;
   VARIABLE L_CU 1 ; VARIABLE B_CU 1 ;
   WRITE 6 '--- GENERATE_M5_FIELD_EXT ...' ;
   {Read parameters from file}
   OPENF 51 'TMP_Enge_params_for_COSY.txt' 'UNKNOWN' ;
   READ 51 ORD ;
   READ 51 LEFF ;
   READ 51 RADIUS ; WRITE 6 ' ORD LEFF RADIUS ' ORD LEFF RADIUS ;
   IEM := 1 ; {magnetic component}
   LOOP IMP 1 10 ;
    LOOP IEE 1 2 ;
     READ 51 IMP ;
     READ 51 IEE ; IEE := IEE+1 ;
     LOOP I 1 12 ;
       READ 51 TMP ; C(I) := TMP ;
     ENDLOOP ;
    {FC IMP IEE IEM C1 C2 C3 C4 C5 C6 ;}
    {WRITE 6 SI(IMP)&' '&SI(IEE)&' '&SI(IEM)&' '&S(C(2)) ;}
     FC IMP IEE IEM  C(1) C(2) C(3) C(4) C(5) C(6) ;
    ENDLOOP ;
   ENDLOOP ;
   {Read normal field components}
   READ 51 STR0 ; {WRITE 6 STR0 ;}
   READ 51 TMP ;
   READ 51 F1 ;
   READ 51 F2 ;
   READ 51 F3 ;
   READ 51 F4 ;
   READ 51 F5 ;
   READ 51 F6 ;
   READ 51 TMP ;
   READ 51 TMP ;
   READ 51 TMP ;
   READ 51 TMP ;
   {Read skew field components}
   READ 51 STR0 ; WRITE 6 STR0 ;
   READ 51 TMP ;
   READ 51 TMP ;
   READ 51 TMP ;
   READ 51 TMP ;
   READ 51 TMP ;
   READ 51 TMP ;
   READ 51 TMP ;
   READ 51 TMP ;
   READ 51 TMP ;
   READ 51 TMP ;
   READ 51 TMP ;
   {Set calculation and M5 parameters for field calculations }
   OV ORD 3 0 ;
   RPR 1 1 1 ; {RP is arbitrary}
   FR 4 ;
   UM ; M5 LEFF F2 F3 F4 F5 F6 RADIUS ;
   {Read positions to evaluate fields at. Evaluate fields. Save to file.}
   READ 51 STR0 ; {WRITE 6 STR0 ;}
   READ 51 STR0 ; {WRITE 6 'COSY creating file: '&STR0 ;}
   OPENF 52 STR0 'UNKNOWN' ;
   STR0 := 'START' ;
   NPNTS := 0 ;
   WHILE NOT( SS(STR0,1,3)='END' );
    READ 51 STR0 ;
    IF NOT( SS(STR0,1,3)='END' );
     NPNTS := NPNTS + 1 ;
     EXTRACT_DELIM_VALUES STR0 ',' 3 XYZ ;
     X := XYZ(1) ; Y := XYZ(2) ; Z := XYZ(3) ;
     M5_POTFLD_OUT  X Y Z   BX BY BZ ;
     WRITE 52 ' '&S(X)&' '&S(Y)&' '&S(Z)&' '&S(BX)&' '&S(BY)&' '&S(BZ) ;
     {IF MOD(NPNTS,1000)=0 ; WRITE 6 ' '&SI(NPNTS) ; ENDIF ;}
    ENDIF ;
   ENDWHILE ;
   CLOSEF 52 ;
   CLOSEF 51 ;
   WRITE 6 '--- GENERATE_M5_FIELD_EXT ... END' ;
  ENDPROCEDURE ;

{+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++}
{+++++++++++++++ MAIN ++++++++++++++++++++++++++++++++++++++++++++++++++++++}
{+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++}
  {INIT_CONTROL_FLAGS ;}
  DEF ;
  GENERATE_M5_FIELD_EXT ;

  WRITE 6 ' ...END ' ;
ENDPROCEDURE ; RUN ; END ;
